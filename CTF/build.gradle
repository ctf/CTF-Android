ext {
    TARGET_SDK = 25
    BUILD_TOOLS = "25.0.2"
    ANDROID_SUPPORT_LIBS = "25.2.0"
    VERSION_CODE = 4
    VERSION_NAME = "1.1"
}

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'com.github.dcendents.android-maven'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}


tasks.whenTaskAdded { task ->
    if (task.name == "lint") {
        task.enabled = false
    } else if (task.name == "lintVitalRelease") {
        task.enabled = false
    }
}

android {
    compileSdkVersion TARGET_SDK
    buildToolsVersion BUILD_TOOLS
    defaultConfig {
        applicationId 'ca.mcgill.science.ctf'
        minSdkVersion 17
        targetSdkVersion TARGET_SDK
        versionCode VERSION_CODE
        versionName VERSION_NAME
        multiDexEnabled true
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    "CTF-Android.apk")
        }
    }

    signingConfigs {
        test {
            storeFile file("../files/test.keystore")
            storePassword "testkey"
            keyAlias "testKey"
            keyPassword "testkey"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "app_name", "CTF Debug"
        }
        releaseTest { //just like release, but with our test keys
            applicationIdSuffix ".test"
            signingConfig signingConfigs.test
            versionNameSuffix "-test"
            resValue "string", "app_name", "CTF Test"
        }
        release {
            minifyEnabled false
            resValue "string", "app_name", "CTF"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'

    //Framework lib with android support libraries and eventbus
    compile('ca.allanwang:capsule:2651a0be0417a17d11b816cbad97f7755b8f4787@aar') {
        transitive = true;
    }

    //Butterknife
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'com.android.volley:volley:1.0.0'
    compile 'com.github.vajro:MaterialDesignLibrary:1.6'

    compile 'com.eclipsesource.jaxrs:jersey-all:2.22.2'

//    compile 'com.squareup.okhttp3:okhttp:3.6.0'
//    compile 'com.google.code.gson:gson:2.8.0'

//    compile 'com.facebook.stetho:stetho:1.4.2'
//    compile 'com.facebook.stetho:stetho-okhttp3:1.4.2'

//    compile 'com.octo.android.robospice:robospice:1.4.14'
//    compile 'com.octo.android.robospice:robospice-okhttp:1.4.14'

    compile 'com.ocpsoft:ocpsoft-pretty-time:1.0.7'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    //starting new dependency model
//    compile "io.reactivex.rxjava2:rxjava:2.0.7"
//    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

dokka {
    includes = ['README.md']

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/CTFMcGill/CTF-Android/tree/master/app/src/main/kotlin"
        suffix = "#L"
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}