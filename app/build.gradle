apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion setup.compileSdk
    buildToolsVersion setup.buildTools


    defaultConfig {
        applicationId "ca.mcgill.science.ctf.android"
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        versionCode release.versionCode
        versionName release.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    def releaseKeystoreFile = file("../files/release.keystore")
    def releasePropFile = file("../files/release.properties")
    def withReleaseSigning = releaseKeystoreFile.exists() && releasePropFile.exists()

    signingConfigs {
        debug {
            storeFile file("../files/debug.keystore")
            storePassword "ctfdebug"
            keyAlias "ctfdebug"
            keyPassword "ctfdebug"
        }

        if (withReleaseSigning) {

            def releaseProps = new Properties()
            releasePropFile.withInputStream { releaseProps.load(it) }

            release {
                storeFile releaseKeystoreFile
                storePassword releaseProps.getProperty('storePassword')
                keyAlias releaseProps.getProperty('keyAlias')
                keyPassword releaseProps.getProperty('keyPassword')
            }
        }

    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "app_name", "CTF Debug"
        }

        release {
            minifyEnabled false
            resValue "string", "app_name", "CTF"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (withReleaseSigning) signingConfig signingConfigs.release
        }
    }

    def srcDirs = [
            'api',
            'application',
            'login',
            'preferences',
            'utils',
    ]

    sourceSets {
        main {
            def namespace = 'ca/mcgill/science/ctf/android'

            manifest.srcFile "kotlin/$namespace/AndroidManifest.xml"
            java.srcDir 'kotlin'
            java.includes = srcDirs.collect { "$namespace/$it/**/*.kt" }
            res.srcDirs = ["kotlin/$namespace/res"]
            srcDirs.forEach {
                res.srcDirs += "kotlin/$namespace/$it/res"
            }
        }
    }

    packagingOptions {
        pickFirst 'META-INF/library_release.kotlin_module'
    }
}

dependencies {
    implementation "androidx.core:core-ktx:${versions.androidX}"
    implementation "androidx.appcompat:appcompat:${versions.androidX}"
    implementation "androidx.recyclerview:recyclerview:${versions.androidX}"
    implementation "androidx.annotation:annotation:${versions.androidX}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}"
    implementation "androidx.cardview:cardview:${versions.androidX}"

    // LiveData + ViewModel

    implementation "android.arch.lifecycle:extensions:${versions.lifecycle}"

    // Navigation

    implementation "android.arch.navigation:navigation-fragment-ktx:${versions.navigation}"
    implementation "android.arch.navigation:navigation-ui-ktx:${versions.navigation}"

    implementation "com.google.android.material:material:${versions.material}"

    implementation "com.mikepenz:iconics-core:${versions.iconics}"
    implementation 'com.mikepenz:google-material-typeface:3.0.1.2.original@aar'

    implementation project(':api-tepid')

    testImplementation 'junit:junit:4.12'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}"
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}